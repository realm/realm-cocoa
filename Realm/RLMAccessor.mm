////////////////////////////////////////////////////////////////////////////
//
// Copyright 2014 Realm Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
////////////////////////////////////////////////////////////////////////////

#import "RLMObject_Private.h"
#import "RLMProperty_Private.h"
#import "RLMObject_Private.h"
#import "RLMArray_Private.hpp"
#import "RLMUtil.hpp"
#import "RLMObjectSchema.h"
#import "RLMObjectStore.h"

#import <objc/runtime.h>

enum RLMAccessorTypes {
    RLMAccessorTypeNormal = 0,
    RLMAccessorTypeInvalid,
    RLMAccessorTypeReadOnly,
    RLMAccessorTypeInsertion,
    RLMAccessorTypeStandalone,
    RLMNumAccessorTypes
};

// accessor caches by type
static NSMapTable *s_accessorCaches[RLMNumAccessorTypes];

// initialize statics
void RLMAccessorCacheInitialize() {
    static dispatch_once_t onceToken;
    dispatch_once(&onceToken, ^{
        for (NSUInteger i = 0; i < RLMNumAccessorTypes; i++) {
            s_accessorCaches[i] = [NSMapTable mapTableWithKeyOptions:NSPointerFunctionsOpaquePersonality
                                                        valueOptions:NSPointerFunctionsOpaquePersonality];
        }
    });
}

// verify attached
inline void RLMVerifyAttached(RLMObject *obj) {
    if (!obj->_row.is_attached()) {
        @throw [NSException exceptionWithName:@"RLMException"
                                       reason:@"Object has been deleted and is no longer valid."
                                     userInfo:nil];
    }
}

// verify writable
inline void RLMVerifyInWriteTransaction(RLMObject *obj) {
    if (!obj->_realm->_inWriteTransaction) {
        @throw [NSException exceptionWithName:@"RLMException"
                                       reason:@"Attempting to modify object outside of a write transaction."
                                     userInfo:nil];
    }
}

// long getter/setter
inline long long RLMGetLong(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    return obj->_row.get_int(col);
}
inline void RLMSetLong(RLMObject *obj, NSUInteger col, long long val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    obj->_row.set_int(col, val);
}

// float getter/setter
inline float RLMGetFloat(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    return obj->_row.get_float(col);
}
inline void RLMSetFloat(RLMObject *obj, NSUInteger col, float val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    obj->_row.set_float(col, val);
}

// double getter/setter
inline double RLMGetDouble(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    return obj->_row.get_double(col);
}
inline void RLMSetDouble(RLMObject *obj, NSUInteger col, double val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    obj->_row.set_double(col, val);
}

// bool getter/setter
inline bool RLMGetBool(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    return obj->_row.get_bool(col);
}
inline void RLMSetBool(RLMObject *obj, NSUInteger col, bool val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    obj->_row.set_bool(col, val);
}

// string getter/setter
inline NSString *RLMGetString(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    return RLMStringDataToNSString(obj->_row.get_string(col));
}
inline void RLMSetString(RLMObject *obj, NSUInteger col, NSString *val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    obj->_row.set_string(col, RLMStringDataWithNSString(val));
}

// date getter/setter
inline NSDate *RLMGetDate(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    tightdb::DateTime dt = obj->_row.get_datetime(col);
    return [NSDate dateWithTimeIntervalSince1970:dt.get_datetime()];
}
inline void RLMSetDate(RLMObject *obj, NSUInteger col, NSDate *date) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    std::time_t time = date.timeIntervalSince1970;
    obj->_row.set_datetime(col, tightdb::DateTime(time));
}

// data getter/setter
inline NSData *RLMGetData(RLMObject *obj, NSUInteger col) {
    RLMVerifyAttached(obj);
    tightdb::BinaryData data = obj->_row.get_binary(col);
    return [NSData dataWithBytes:data.data() length:data.size()];
}
inline void RLMSetData(RLMObject *obj, NSUInteger col, NSData *data) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);
    obj->_row.set_binary(col, RLMBinaryDataForNSData(data));
}

// link getter/setter
inline RLMObject *RLMGetLink(RLMObject *obj, NSUInteger col, NSString *objectClassName) {
    RLMVerifyAttached(obj);

    if (obj->_row.is_null_link(col)) {
        return nil;
    }
    NSUInteger index = obj->_row.get_link(col);
    return RLMCreateObjectAccessor(obj.realm, objectClassName, index);
}
inline void RLMSetLink(RLMObject *obj, NSUInteger col, RLMObject *link) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);

    if (!link || link.class == NSNull.class) {
        // if null
        obj->_row.nullify_link(col);
    }
    else {
        // add to Realm if not in it.
        if (link.realm != obj.realm) {
            [obj.realm addObject:link];
        }
        // set link
        obj->_row.set_link(col, link->_row.get_index());
    }
}

// array getter/setter
inline RLMArray *RLMGetArray(RLMObject *obj, NSUInteger col, NSString *objectClassName) {
    RLMVerifyAttached(obj);

    tightdb::LinkViewRef linkView = obj->_row.get_linklist(col);
    RLMArrayLinkView *ar = [RLMArrayLinkView arrayWithObjectClassName:objectClassName
                                                                 view:linkView
                                                                realm:obj.realm];
    return ar;
}
inline void RLMSetArray(RLMObject *obj, NSUInteger col, id<NSFastEnumeration> val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);

    tightdb::LinkViewRef linkView = obj->_row.get_linklist(col);
    // remove all old
    // FIXME: make sure delete rules don't purge objects
    linkView->clear();
    for (RLMObject *link in val) {
        // add to realm if needed
        if (link.realm != obj.realm) {
            [obj.realm addObject:link];
        }
        // set in link view
        linkView->add(link->_row.get_index());
    }
}

// any getter/setter
inline id RLMGetAnyProperty(RLMObject *obj, NSUInteger col_ndx) {
    RLMVerifyAttached(obj);

    tightdb::Mixed mixed = obj->_row.get_mixed(col_ndx);
    switch (mixed.get_type()) {
        case RLMPropertyTypeString:
            return RLMStringDataToNSString(mixed.get_string());
        case RLMPropertyTypeInt: {
            return @(mixed.get_int());
        case RLMPropertyTypeFloat:
            return @(mixed.get_float());
        case RLMPropertyTypeDouble:
            return @(mixed.get_double());
        case RLMPropertyTypeBool:
            return @(mixed.get_bool());
        case RLMPropertyTypeDate:
            return [NSDate dateWithTimeIntervalSince1970:mixed.get_datetime().get_datetime()];
        case RLMPropertyTypeData: {
            tightdb::BinaryData bd = mixed.get_binary();
            NSData *d = [NSData dataWithBytes:bd.data() length:bd.size()];
            return d;
        }
        case RLMPropertyTypeArray:
            @throw [NSException exceptionWithName:@"RLMNotImplementedException"
                                           reason:@"RLMArray not yet supported" userInfo:nil];

            // for links and other unsupported types throw
        case RLMPropertyTypeObject:
        default:
            @throw [NSException exceptionWithName:@"RLMException" reason:@"Invalid data type for RLMPropertyTypeAny property." userInfo:nil];
        }
    }
}
inline void RLMSetAnyProperty(RLMObject *obj, NSUInteger col_ndx, id val) {
    RLMVerifyAttached(obj);
    RLMVerifyInWriteTransaction(obj);

    // FIXME - enable when Any supports links
    //    if (obj == nil) {
    //        table.nullify_link(col_ndx, row_ndx);
    //        return;
    //    }
    if ([val isKindOfClass:[NSString class]]) {
        obj->_row.set_mixed(col_ndx, RLMStringDataWithNSString(val));
        return;
    }
    if ([val isKindOfClass:[NSDate class]]) {
        obj->_row.set_mixed(col_ndx, tightdb::DateTime(time_t([(NSDate *)val timeIntervalSince1970])));
        return;
    }
    if ([val isKindOfClass:[NSData class]]) {
        obj->_row.set_mixed(col_ndx, RLMBinaryDataForNSData(val));
        return;
    }
    if ([val isKindOfClass:[NSNumber class]]) {
        const char *data_type = [(NSNumber *)val objCType];
        const char dt = data_type[0];
        switch (dt) {
            case 'i':
            case 's':
            case 'l':
                obj->_row.set_mixed(col_ndx, (int64_t)[(NSNumber *)val longValue]);
                return;
            case 'f':
                obj->_row.set_mixed(col_ndx, [(NSNumber *)val floatValue]);
                return;
            case 'd':
                obj->_row.set_mixed(col_ndx, [(NSNumber *)val doubleValue]);
                return;
            case 'B':
            case 'c':
                obj->_row.set_mixed(col_ndx, [(NSNumber *)val boolValue] == YES);
                return;
        }
    }
    @throw [NSException exceptionWithName:@"RLMException" reason:@"Inserting invalid object for RLMPropertyTypeAny property" userInfo:nil];
}

// dynamic getter with column closure
IMP RLMAccessorGetter(NSUInteger col, char accessorCode, NSString *objectClassName) {
    switch (accessorCode) {
        case 'i':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return (int)RLMGetLong(obj, col);
            });
        case 'l':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetLong(obj, col);
            });
        case 'f':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetFloat(obj, col);
            });
        case 'd':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetDouble(obj, col);
            });
        case 'B':
        case 'c':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetBool(obj, col);
            });
        case 's':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetString(obj, col);
            });
        case 'a':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetDate(obj, col);
            });
        case 'e':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetData(obj, col);
            });
        case 'k':
            return imp_implementationWithBlock(^id(RLMObject *obj) {
                return RLMGetLink(obj, col, objectClassName);
            });
        case 't':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetArray(obj, col, objectClassName);
            });
        case '@':
            return imp_implementationWithBlock(^(RLMObject *obj) {
                return RLMGetAnyProperty(obj, col);
            });
        default:
            @throw [NSException exceptionWithName:@"RLMException" reason:@"Invalid accessor code" userInfo:nil];
    }
}

// dynamic setter with column closure
IMP RLMAccessorSetter(NSUInteger col, char accessorCode) {
    switch (accessorCode) {
        case 'i':
            return imp_implementationWithBlock(^(RLMObject *obj, int val) {
                RLMSetLong(obj, col, val);
            });
        case 'l':
            return imp_implementationWithBlock(^(RLMObject *obj, long val) {
                RLMSetLong(obj, col, val);
            });
        case 'f':
            return imp_implementationWithBlock(^(RLMObject *obj, float val) {
                RLMSetFloat(obj, col, val);
            });
        case 'd':
            return imp_implementationWithBlock(^(RLMObject *obj, double val) {
                RLMSetDouble(obj, col, val);
            });
        case 'B':
            return imp_implementationWithBlock(^(RLMObject *obj, bool val) {
                RLMSetBool(obj, col, val);
            });
        case 'c':
            return imp_implementationWithBlock(^(RLMObject *obj, BOOL val) {
                RLMSetBool(obj, col, val);
            });
        case 's':
            return imp_implementationWithBlock(^(RLMObject *obj, NSString *val) {
                RLMSetString(obj, col, val);
            });
        case 'a':
            return imp_implementationWithBlock(^(RLMObject *obj, NSDate *date) {
                RLMSetDate(obj, col, date);
            });
        case 'e':
            return imp_implementationWithBlock(^(RLMObject *obj, NSData *data) {
                RLMSetData(obj, col, data);
            });
        case 'k':
            return imp_implementationWithBlock(^(RLMObject *obj, RLMObject *link) {
                RLMSetLink(obj, col, link);
            });
        case 't':
            return imp_implementationWithBlock(^(RLMObject *obj, id<NSFastEnumeration> val) {
                RLMSetArray(obj, col, val);
            });
        case '@':
            return imp_implementationWithBlock(^(RLMObject *obj, id val) {
                RLMSetAnyProperty(obj, col, val);
            });
        default:
            @throw [NSException exceptionWithName:@"RLMException"
                                           reason:@"Invalid accessor code"
                                         userInfo:nil];
    }
}

// getter for standalone
IMP RLMAccessorStandaloneGetter(NSUInteger col, char accessorCode, NSString *objectClassName) {
    // only override getters for RLMArray properties
    if (accessorCode == 't') {
        return imp_implementationWithBlock(^(RLMObject *obj) {
            typedef id (*getter_type)(RLMObject *, SEL);
            RLMProperty *prop = obj.RLMObject_schema.properties[col];
            Class superClass = class_getSuperclass(obj.class);
            getter_type superGetter = (getter_type)class_getMethodImplementation(superClass, NSSelectorFromString(prop.getterName));
            id val = superGetter(obj, NSSelectorFromString(prop.getterName));
            if (!val) {
                SEL setterSel = NSSelectorFromString(prop.setterName);
                typedef void (*setter_type)(RLMObject *, SEL, id);
                setter_type setter = (setter_type)class_getMethodImplementation(obj.class, setterSel);
                val = [RLMArray standaloneArrayWithObjectClassName:objectClassName];
                setter(obj, setterSel, val);
            }
            return val;
        });
    }
    return nil;
}

// macros/helpers to generate objc type strings for registering methods
#define GETTER_TYPES(C) C ":@"
#define SETTER_TYPES(C) "v:@" C

// getter type strings
// NOTE: this typecode is really the the first charachter of the objc/runtime.h type
//       the @ type maps to multiple tightdb types (string, date, array, mixed, any which are id in objc)
const char * getterTypeStringForObjcCode(char code) {
    switch (code) {
        case 'i': return GETTER_TYPES("i");
        case 'l': return GETTER_TYPES("l");
        case 'f': return GETTER_TYPES("f");
        case 'd': return GETTER_TYPES("d");
        case 'B': return GETTER_TYPES("B");
        case 'c': return GETTER_TYPES("c");
        case '@': return GETTER_TYPES("@");
        default: @throw [NSException exceptionWithName:@"RLMException" reason:@"Invalid accessor code" userInfo:nil];
    }
}

// setter type strings
// NOTE: this typecode is really the the first charachter of the objc/runtime.h type
//       the @ type maps to multiple tightdb types (string, date, array, mixed, any which are id in objc)
const char * setterTypeStringForObjcCode(char code) {
    switch (code) {
        case 'i': return SETTER_TYPES("i");
        case 'l': return SETTER_TYPES("l");
        case 'f': return SETTER_TYPES("f");
        case 'd': return SETTER_TYPES("d");
        case 'B': return SETTER_TYPES("B");
        case 'c': return SETTER_TYPES("c");
        case '@': return SETTER_TYPES("@");
        default: @throw [NSException exceptionWithName:@"RLMException" reason:@"Invalid accessor code" userInfo:nil];
    }
}

// get accessor lookup code based on objc type and rlm type
char accessorCodeForType(char objcTypeCode, RLMPropertyType rlmType) {
    switch (objcTypeCode) {
        case 'q': return 'l';   // long long same as long
        case '@':               // custom accessors for strings and subtables
            switch (rlmType) {  // custom accessor codes for types that map to objc objects
                case RLMPropertyTypeObject: return 'k';
                case RLMPropertyTypeString: return 's';
                case RLMPropertyTypeArray: return 't';
                case RLMPropertyTypeDate: return 'a';
                case RLMPropertyTypeData: return 'e';
                case RLMPropertyTypeAny: return '@';
                    
                // throw for all primitive types
                case RLMPropertyTypeBool:
                case RLMPropertyTypeDouble:
                case RLMPropertyTypeFloat:
                case RLMPropertyTypeInt:
                    @throw [NSException exceptionWithName:@"RLMException" reason:@"Invalid type for objc typecode" userInfo:nil];
            }
        default:
            return objcTypeCode;
    }
}

// implement the class method className on accessors to return the className of the
// base object
inline void RLMImplementClassNameMethod(Class accessorClass, NSString *className) {
    Class metaClass = objc_getMetaClass(class_getName(accessorClass));
    IMP imp = imp_implementationWithBlock(^{ return className; });
    class_replaceMethod(metaClass, @selector(className), imp, "@:");
}

Class RLMCreateAccessorClass(Class objectClass,
                             RLMObjectSchema *schema,
                             NSString *accessorClassPrefix,
                             IMP (*getterGetter)(NSUInteger, char, NSString *),
                             IMP (*setterGetter)(NSUInteger, char),
                             NSMapTable *cache) {
    // return cached
    if (Class cls = [cache objectForKey:objectClass]) {
        return cls;
    }

    // if objectClass is RLMObject then don't create custom accessor (only supports dynamic interface)
    if (objectClass == RLMObject.class) {
        [cache setObject:objectClass forKey:objectClass];
        return objectClass;
    }
    
    // throw if no schema, prefix, or object class
    if (!objectClass || !schema || !accessorClassPrefix) {
        @throw [NSException exceptionWithName:@"RLMInternalException" reason:@"Missing arguments" userInfo:nil];
    }
    
    // if objectClass is a dicrect RLMSubclass use it, otherwise use proxy class
    if (class_getSuperclass(objectClass) != RLMObject.class) {
        @throw [NSException exceptionWithName:@"RLMException" reason:@"objectClass must derive from RLMObject" userInfo:nil];
    }
    
    // create and register proxy class which derives from object class
    NSString *objectClassName = NSStringFromClass(objectClass);
    NSString *accessorClassName = [accessorClassPrefix stringByAppendingString:objectClassName];
    Class accClass = objc_allocateClassPair(objectClass, accessorClassName.UTF8String, 0);
    objc_registerClassPair(accClass);
    
    // override getters/setters for each propery
    for (unsigned int propNum = 0; propNum < schema.properties.count; propNum++) {
        RLMProperty *prop = schema.properties[propNum];
        char accessorCode = accessorCodeForType(prop.objcType, prop.type);
        if (getterGetter) {
            SEL getterSel = NSSelectorFromString(prop.getterName);
            IMP getterImp = getterGetter(prop.column, accessorCode, prop.objectClassName);
            if (getterImp) {
                class_replaceMethod(accClass, getterSel, getterImp, getterTypeStringForObjcCode(prop.objcType));
            }
        }
        if (setterGetter) {
            SEL setterSel = NSSelectorFromString(prop.setterName);
            IMP setterImp = setterGetter(prop.column, accessorCode);
            if (setterImp) {
                class_replaceMethod(accClass, setterSel, setterImp, setterTypeStringForObjcCode(prop.objcType));
            }
        }
    }
    
    // implement className for accessor to return base className
    RLMImplementClassNameMethod(accClass, schema.className);
    
    // cache and return
    [cache setObject:accClass forKey:objectClass];
    return accClass;
}

Class RLMAccessorClassForObjectClass(Class objectClass, RLMObjectSchema *schema) {
    return RLMCreateAccessorClass(objectClass, schema, @"RLMAccessor_",
                                  RLMAccessorGetter, RLMAccessorSetter, s_accessorCaches[RLMAccessorTypeNormal]);
}

Class RLMStandaloneAccessorClassForObjectClass(Class objectClass, RLMObjectSchema *schema) {
    return RLMCreateAccessorClass(objectClass, schema, @"RLMStandalone_",
                                  RLMAccessorStandaloneGetter, NULL, s_accessorCaches[RLMAccessorTypeStandalone]);
}

// Dynamic accessor name for a classname
inline NSString *RLMDynamicClassName(NSString *className, NSUInteger version) {
    return [NSString stringWithFormat:@"RLMDynamic_%@_Version_%lu", className, (unsigned long)version];
}

void RLMDynamicSet(RLMObject *obj, NSString *propName, id val, BOOL validate) {
    RLMProperty *prop = obj.RLMObject_schema[propName];
    if (!prop) {
        @throw [NSException exceptionWithName:@"RLMException"
                                       reason:@"Invalid property name"
                                     userInfo:@{@"Property name:" : propName ? propName : @"nil",
                                                @"Class name": [obj.class className]}];
    }
    if (validate && !RLMIsObjectValidForProperty(val, prop)) {
        @throw [NSException exceptionWithName:@"RLMException"
                                       reason:@"Invalid value for property"
                                     userInfo:@{@"Property name:" : propName ? propName : @"nil",
                                                @"Value": val ? [val description] : @"nil"}];
    }
    NSUInteger col = prop.column;
    switch (accessorCodeForType(prop.objcType, prop.type)) {
        case 'i':
        case 'l':
            RLMSetLong(obj, col, [val longLongValue]);
            break;
        case 'f':
            RLMSetFloat(obj, col, [val floatValue]);
            break;
        case 'd':
            RLMSetDouble(obj, col, [val doubleValue]);
            break;
        case 'B':
        case 'c':
            RLMSetBool(obj, col, (bool)[val boolValue]);
            break;
        case 's':
            RLMSetString(obj, col, val);
            break;
        case 'a':
            RLMSetDate(obj, col, val);
            break;
        case 'e':
            RLMSetData(obj, col, val);
            break;
        case 'k':
            RLMSetLink(obj, col, val);
            break;
        case 't':
            RLMSetArray(obj, col, val);
            break;
        case '@':
            RLMSetAnyProperty(obj, col, val);
            break;
        default:
            @throw [NSException exceptionWithName:@"RLMException"
                                           reason:@"Invalid accessor code"
                                         userInfo:nil];
    }
}

id RLMDynamicGet(RLMObject *obj, NSString *propName) {
    RLMProperty *prop = obj.RLMObject_schema[propName];
    if (!prop) {
        @throw [NSException exceptionWithName:@"RLMException"
                                       reason:@"Invalid property name"
                                     userInfo:@{@"Property name:" : propName ? propName : @"nil",
                                                @"Class name": [obj.class className]}];
    }
    NSUInteger col = prop.column;
    switch (accessorCodeForType(prop.objcType, prop.type)) {
        case 'i': return @((int)RLMGetLong(obj, col));
        case 'l': return @(RLMGetLong(obj, col));
        case 'f': return @(RLMGetFloat(obj, col));
        case 'd': return @(RLMGetDouble(obj, col));
        case 'B': return @(RLMGetBool(obj, col));
        case 'c': return @(RLMGetBool(obj, col));
        case 's': return RLMGetString(obj, col);
        case 'a': return RLMGetDate(obj, col);
        case 'e': return RLMGetData(obj, col);
        case 'k': return RLMGetLink(obj, col, prop.objectClassName);
        case 't': return RLMGetArray(obj, col, prop.objectClassName);
        case '@': return RLMGetAnyProperty(obj, col);
        default:
            @throw [NSException exceptionWithName:@"RLMException"
                                           reason:@"Invalid accessor code"
                                         userInfo:nil];
    }
}


